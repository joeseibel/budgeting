grammar budgeting.Budgeting with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate budgeting "http://www.Budgeting.budgeting"

BudgetingFile:
	Library | Year
;

Library:
	'library' name=ID '{'
		categories+=Category*
	'}'
;

Category:
	(income?='income' | 'expense') name=ID
;

Year:
	name=INT 'uses' library=[Library] '{'
		months+=Month*
	'}'
;

Month:
	name=MonthEnum 'budget' '{'
		budgetEntries+=BudgetEntry*
	'}' 'actual' '{'
		actualEntries+=ActualEntry*
	'}'
;

enum MonthEnum:
	JANUARY='january' |
	FEBRUARY='february' |
	MARCH='march' |
	APRIL='april' |
	MAY='may' |
	JUNE='june' |
	JULY='july' |
	AUGUST='august' |
	SEPTEMBER='september' |
	OCTOBER='october' |
	NOVEMBER='november' |
	DECEMBER='december'
;

BudgetEntry:
	{BudgetAmountEntry} category=[Category] ':' amount=Dollar |
	{BudgetFactorEntry} category=[Category] ':' baseEntry=[BudgetEntry] '*' factor=Double
;

ActualEntry:
	{ActualAmountEntry} category=[Category] ':' amount=Dollar |
	{ActualTransactionEntry} category=[Category] '{'
		transactions+=Transaction+
	'}'
;

Transaction:
	{CashTransaction} 'cash' amount=Dollar ('on' day=INT)? |
	{CardTransaction} 'card' amount=Dollar 'on' day=INT 'from' from=STRING
;

Entry:
	category=[Category] ':' amount=Dollar
;

Dollar returns ecore::ELong:
	DECIMAL
;

Double returns ecore::EDouble:
	INT | DECIMAL
;

terminal DECIMAL:
	INT '.' INT
;