/*
 * generated by Xtext
 */
package budgeting.ui.labeling

import budgeting.budgeting.ActualAmountEntry
import budgeting.budgeting.ActualTransactionEntry
import budgeting.budgeting.BudgetAmountEntry
import budgeting.budgeting.BudgetFactorEntry
import budgeting.budgeting.ExpenseCategory
import budgeting.budgeting.IncomeCategory
import budgeting.budgeting.Month
import budgeting.services.BudgetingGrammarAccess
import com.google.inject.Inject
import org.eclipse.emf.edit.ui.provider.AdapterFactoryLabelProvider
import org.eclipse.jface.viewers.StyledString
import org.eclipse.jface.viewers.StyledString.Styler
import org.eclipse.swt.SWT
import org.eclipse.swt.graphics.RGB
import org.eclipse.swt.widgets.Display
import org.eclipse.xtext.conversion.IValueConverterService
import org.eclipse.xtext.ui.editor.utils.TextStyle
import org.eclipse.xtext.ui.label.DefaultEObjectLabelProvider
import org.eclipse.xtext.ui.label.StylerFactory

/**
 * Provides labels for EObjects.
 * 
 * See https://www.eclipse.org/Xtext/documentation/304_ide_concepts.html#label-provider
 */
class BudgetingLabelProvider extends DefaultEObjectLabelProvider {
	val static Styler RED_STYLER = [foreground = Display.^default.getSystemColor(SWT.COLOR_RED)]
	
	@Inject
	IValueConverterService converterService
	@Inject
	BudgetingGrammarAccess grammarAccess
	@Inject
	StylerFactory stylerFactory
	
	@Inject
	new(AdapterFactoryLabelProvider delegate) {
		super(delegate);
	}
	
	def StyledString text(ExpenseCategory expenseCategory) {
		new StyledString(expenseCategory.name, RED_STYLER)
	}
	
	def String text(Month month) {
		month.name.getLiteral
	}
	
	def Object text(BudgetAmountEntry budgetAmountEntry) {
		switch category : budgetAmountEntry.category {
			case category.eIsProxy: "<unknown>: " + budgetAmountEntry.amount.toDollar
			IncomeCategory: category.name + ": " + budgetAmountEntry.amount.toDollar
			ExpenseCategory: new StyledString(category.name + ": ").append(budgetAmountEntry.amount.toDollar, RED_STYLER)
		}
	}
	
	def StyledString text(BudgetFactorEntry budgetFactorEntry) {
		new StyledString((budgetFactorEntry.category.name ?: "<unknown>") + ": ").append('''«budgetFactorEntry.baseEntry?.category?.name ?: "<unknown>"» * «budgetFactorEntry.factor»''', RED_STYLER)
	}
	
	def Object text(ActualAmountEntry actualAmountEntry) {
		switch category : actualAmountEntry.category {
			case category.eIsProxy: "<unknown>: " + actualAmountEntry.amount.toDollar
			IncomeCategory: category.name + ": " + actualAmountEntry.amount.toDollar
			ExpenseCategory: new StyledString(category.name + ": ").append(actualAmountEntry.amount.toDollar, RED_STYLER)
		}
	}
	
	def StyledString text(ActualTransactionEntry actualTransactionEntry) {
		val italicRedStyler = stylerFactory.createXtextStyleAdapterStyler(new TextStyle => [
			style = SWT.ITALIC
			color = new RGB(255, 0, 0)
		])
		new StyledString((actualTransactionEntry.category.name ?: "<unknown>") + ": ").append(actualTransactionEntry.transactions.fold(0L, [$0 + $1.amount]).toDollar, italicRedStyler)
	}
	
	def private toDollar(long amount) {
		converterService.toString(amount, grammarAccess.dollarRule.name)
	}
}